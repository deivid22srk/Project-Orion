import java.net.URL
import java.nio.file.Files
import java.nio.file.Paths
import java.security.MessageDigest

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

tasks.register("downloadImageFS") {
    def assetDir = Paths.get("app", "src", "main", "assets").toFile()
    File imageFS = new File(assetDir, "imagefs.txz")
    String imageFSSHA256Sum = "https://gitlab.com/winlator3/winlator-extra/-/raw/main/imagefs/imagefs.txz.sha256sum"
    int parts = 4

    doLast {
        if (!imageFS.exists()) {
            println("ImageFS was not present, downloading...\n")
            FileOutputStream outputStream = new FileOutputStream(imageFS)
            for (int i = 0; i < parts; i++) {
                File partFile = new File(assetDir, String.format("imagefs.txz.%02d", i))
                String partURL = String.format("https://gitlab.com/winlator3/winlator-extra/-/raw/main/imagefs/imagefs.txz.%02d", i)
                new URL(partURL).withInputStream { p ->
                    partFile.withOutputStream { it << p }
                    outputStream << partFile.bytes
                }
                partFile.delete()
            }
            outputStream.close()
        }
    }
}

tasks.register("downloadProton") {
    def assetDir = Paths.get("app", "src", "main", "assets").toFile();
    File protonARM64EC = new File(assetDir, "proton-9.0-arm64ec.txz")
    File protonX8664 = new File(assetDir, "proton-9.0-x86_64.txz")
    String protonARM64ECUrl = "https://gitlab.com/winlator3/winlator-extra/-/raw/main/proton/proton-9.0-arm64ec.txz"
    String protonARM64ECSHA256Sum = "https://gitlab.com/winlator3/winlator-extra/-/raw/main/proton/proton-9.0-arm64ec.txz.sha256sum"
    String protonX8664Url = "https://gitlab.com/winlator3/winlator-extra/-/raw/main/proton/proton-9.0-x86_64.txz"
    String protonX8664SHA256Sum = "https://gitlab.com/winlator3/winlator-extra/-/raw/main/proton/proton-9.0-x86_64.txz.sha256sum"

    doLast {
        if (!protonARM64EC.exists()) {
            println("Proton 9.0 ARM64EC was not present, downloading...\n")
            new URL(protonARM64ECUrl).withInputStream { i -> protonARM64EC.withOutputStream { it << i } }
        }
        if (!protonX8664.exists()) {
            println("Proton 9.0 x86_64 was not present, downloading...\n")
            new URL(protonX8664Url).withInputStream { i -> protonX8664.withOutputStream { it << i } }
        }
    }
}

tasks.register("prepareKotlinBuildScriptModel"){}
downloadProton.dependsOn(downloadImageFS)
preBuild.dependsOn(downloadProton)

android {
    namespace 'com.winlator.cmod'
    compileSdk 34

    defaultConfig {
        applicationId "com.winlator.cmod"
        minSdkVersion 26
        targetSdkVersion 28

        versionCode 20
        versionName "7.1.4x-cmod"

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }

        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.15'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
    }

    ndkVersion '27.0.12077973'

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    
    def composeBom = platform('androidx.compose:compose-bom:2024.02.00')
    implementation composeBom
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    implementation 'androidx.navigation:navigation-compose:2.7.7'
    
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.github.luben:zstd-jni:1.5.2-3@aar'
    implementation 'org.tukaani:xz:1.7'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation files('libs/MidiSynth/MidiSynth.jar')
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.68'
    implementation 'org.openjsse:openjsse:1.1.7'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'io.coil-kt:coil-compose:2.5.0'
    implementation 'com.kohlschutter.junixsocket:junixsocket-common:2.6.0'
    implementation 'com.kohlschutter.junixsocket:junixsocket-native-common:2.6.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
}
